plugins {
	id 'io.spring.nohttp' version '0.0.4.RELEASE'
	id 'io.spring.javaformat' apply false
}

nohttp {
	source.exclude "buildSrc/**"
}

subprojects {
	group = 'io.spring.asciidoctor'

	apply plugin: 'checkstyle'
	apply plugin: 'io.spring.javaformat'
	apply plugin: 'java'

	sourceCompatibility = '1.8'
	targetCompatibility = '1.8'

	repositories {
		mavenCentral()
	}

	dependencies {
		checkstyle "io.spring.javaformat:spring-javaformat-checkstyle:$springJavaFormatVersion"
		testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
		testImplementation 'org.assertj:assertj-core:3.19.0'
		testRuntimeOnly("org.junit.platform:junit-platform-launcher:1.7.1")
	}

	test {
		useJUnitPlatform()
	}

	java {
		withJavadocJar()
		withSourcesJar()
	}
}
